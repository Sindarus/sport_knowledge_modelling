#DECLARATION DES PREFIXES
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX moi: <http://www.semanticweb.org/germain/ontologies/2016/10/CLAUSS_SAINTIER_Sports#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

#REQUETE 1 : Sportifs et leurs nationalités.
SELECT ?sportif ?nationalite
WHERE { 
	?sportif moi:personneANationalite ?nationalite.
	?sportif moi:personneAPlace ?c
} ORDER BY ?a

#REQUETE 2 : Intitulés de tous les événements sportifs répertoriés pour l'année 2016
SELECT ?intitule_evenement
WHERE {
	?x moi:evenementAIntitule ?intitule_evenement.
	?x moi:evenementAAnnee 2016
}

#REQUETE 3 : Tous les noms de famille de sportifs commençant par m (ou M), triés par ordre alphabétique.
SELECT ?nom
WHERE
{
    ?x moi:personneAPlace ?c .
    ?x foaf:lastName ?nom .
    FILTER regex(?nom, "^m", "i") .
}
ORDER BY ?nom

#REQUETE 4 : Combien y a-t-il de meneurs dans l'équipe masculine de la SIG ?
SELECT (COUNT(?x) as ?nb_meneurs)
WHERE
{
    ?x moi:personneAPlace ?c .
    ?x moi:sportifEstDansEquipe moi:SIG_Basket .
    ?c moi:placeEstPoste moi:Meneur
}

#REQUETE 5 : Equipes pour lesquelles le capitaine n'est pas connu.
SELECT ?team
WHERE {
	?team a moi:Equipe.
	FILTER NOT EXISTS {
		?team moi:equipeASportif ?sportif.
		?sportif moi:personneAPlace ?place.
		?place moi:placeEstRole moi:Capitaine
	}
}

#REQUETE 6 : Pour toutes les nationalités avec plus de 3 sportifs, nombre de ces sportifs ?
SELECT ?pays (COUNT(?x) as ?nb_joueurs)
WHERE
{
    ?x moi:personneAPlace ?c .
    ?x moi:personneANationalite ?pays
}
GROUP BY ?pays
HAVING (COUNT(?x) > 3)

#REQUETE 7 : Y a-t-il un joueur finlandais dans l'équipe masculine de la SIG ?
SELECT (COUNT(?x)>=1 as ?nb_joueurs)
WHERE
{
    ?x moi:personneAPlace ?c .
    ?x moi:sportifEstDansEquipe moi:SIG_Basket .
    ?x moi:personneANationalite moi:finlandais
}


#REQUETE 8 : Pour toutes les équipes, le club, l'équipe et nom du capitaine s'il est connu.
SELECT ?nomEquipe ?club ?prenomCapitaine ?nomCapitaine WHERE {
    ?team a moi:Equipe.
    ?team foaf:name ?nomEquipe.
    OPTIONAL {
        ?team moi:equipeAClub ?club.
    }
    OPTIONAL {
        ?team moi:equipeASportif ?sportif.
        ?sportif moi:personneAPlace ?place.
        ?place moi:placeEstRole moi:Capitaine.
        ?sportif foaf:firstName ?prenomCapitaine.
        ?sportif foaf:lastName ?nomCapitaine
    }
}